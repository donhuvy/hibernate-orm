/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later
 * See the lgpl.txt file in the root directory or http://www.gnu.org/licenses/lgpl-2.1.html
 */

apply plugin: 'base'

ext {
	hibernateVersion = '5.3.0-SNAPSHOT'
	baselineJavaVersion = '1.8'
	jpaVersion = new JpaVersion('2.2')

	final String[] hibernateVersionComponents = project.hibernateVersion.split( '\\.' )
	hibernateMajorMinorVersion = hibernateVersionComponents[0] + '.' + hibernateVersionComponents[1]
	hibernateMajorVersion = hibernateVersionComponents[0]

	isSnapshot = project.hibernateVersion.endsWith( "-SNAPSHOT" )
}

group = 'org.hibernate'
version = project.hibernateVersion

class JpaVersion {
	/** The *normal* name (1.0, 2.0, ..) */
	final String name;

	final String osgiName

	JpaVersion(String version){
		this.name = version
		this.osgiName = version + ".0"
	}

	@Override
	String toString() {
		return name
	}
}
